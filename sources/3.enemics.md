# Afegint Enemics al Joc

Al projecte proporcionat ja disposeu d'una escena amb l'enemic (*Characters/Enemy/enemic.tscn*), amb la mateixa estructura que el jugador (*CharacterBody2D*, compost de *AnimationSprite2D* i *CollissionShape2D*).

## Escena per testejar l'enemic

Disposeu d'altra escena *levels/EnemyTest.tscn* amb un escenari amb l'enemic, per tal de treballarel seu moviment.

!!!question "TO-DO"
     Arrossega l'escena de la tortuga a aquest nivell per començr a treballara mb ella.

L'enemic es mourà de manera automàtica d'una banda a l'altra fins que col·lissione amb una paret. Si el personatge li bota a sobre, l'enemic mor, però si el personatge col·lissiona lateralment amb l'enemic, el jugador morirà i es reiniciarà.

Veiem com afegir aquest moviment a l'enemic. En aques cas anem a afegir un nou script buit, sense partir de cap plantilla, i afegirem el següent codi:

```gdscript
extends CharacterBody2D

const SPEED = 100  # Velocitat de l'enemic
var direction = -1 # Velocitat inicial: movent-se cap a l'esquerra

@onready var animated_sprite = $AnimatedSprite2D  # Referència a l'AnimatedSprite

func _ready() -> void:
	# Activem l'animacio
	if(animated_sprite):    # Comprovem primer que estiga inicialitzat
		animated_sprite.play("run")

# Funció que es crida cada fotograma
func _physics_process(delta):
	# Efecte de la gravetat
	if not is_on_floor():
		velocity += get_gravity() * delta
		
	# Movem l'enemic en la direcció definida
	velocity.x = direction * SPEED
	
	# Ajustem el flip horitzontal
	animated_sprite.flip_h = velocity.x>0 # Flip horitzontal si es mou cap a l'esquerra

	# Movem l'enemic
	move_and_slide()
	
	# Si col·lideix amb una paret, canviem de direcció
	# Es important comprovar aço despres del move_and_slide
	if is_on_wall():
		direction = -direction  # Canviem la direcció (de dreta a esquerra o viceversa)
```

El codi és similar al del moviment del pesonatge. Observeu que ara implementem les funcions del bucle del joc `_ready()`, que es dispara quan el node es troba instanciat i preparat, i `_physics_process()`, per tal de processar les físiques.

Després comprovem si no està tocant el terra, per aplicar-li la gravetat, ajustem el *flip* en horitzontal i movem el node amb `move_and_slide()`. Una vegada s'ha mogut, és possible que col·lissione amb alguna paret, de manera que ho comprovem amb `is_on_wall()`. Si és així, simplement fem que canvie de direcció.

!!!note `is_on_wall()` i `is_on_floor()`
     Aquests són dos mètodes especialitzats per a la detecció de col·lissions. En general, quan es detecten col·lissions, necessitem saber el sentit en què s'han produit, ja que no és el mateix caure sobre un enemic per eliminar-lo que xocar contra ell, cas al qual mor el personatge.

     En tots dos casos es produeix una col·lissió, però en el primer es fa en sentit vertical, i en el segon en horitzontal. 

      Aquest sentit ve determinat pel vector *normal* a la col·lissió, un vector perpendicular a la superfície de la col·lissió. Si la normal és més o menys *vertical*, sabem que el personatge està tocant el terra, mentre que si la normal és més o menys *horitzontal*, el personatge està tocant una paret o un enemic.
      
      Les funcions `is_on_floor()` i `is_on_wall` utilitzen aquesta normal internament, i ens simplifiquen la feina de detectar col·lissions en sentit vertical o horitzontal.

Si ara llancem l'escena, veurem que la tortuga canvia de sentit quan col·lissiona horitzontalment amb qualsevol element (personatge inclòs).

Ara ens queda detectar les col·lissions amb el jugador. Per a això ja necessitarem fer ús del vector *normal* en lloc de `is_on_wall()`. Així, que eliminem el següent fragment de codi:

```gdscript
if is_on_wall():
	direction = -direction  # Canviem la direcció (de dreta a esquerra o viceversa)
```

I incorporem el següent:

```gdscript
var collission= get_slide_collision(0)
if collission:
    var objecteCollissionat=collission.get_collider()
	var collision_normal = collission.get_normal()  # Normal de la col·lisió
	
	# Si l'objecte col·lisionat és una paret (normal horitzontal)
	if collision_normal.x != 0:
		direction = -direction  # Invertim la direcció (de dreta a esquerra o viceversa)

	# Si l'objecte col·lisionat és el jugador
	if objecteCollissionat.is_in_group("player"):
	
		if collision_normal.y > 0:  # Si el jugador està caient sobre l'enemic
			queue_free()  # Eliminar l'enemic
		else:  # Si el jugador col·lideix lateralment amb l'enemic
			print("mata jugador")
			get_tree().call_group("player", "reset_position")  # El jugador torna a la posició original
```

Examinem algunes de les claus d'aquest codi.

```gdscript
var collission = get_slide_collision(0)
```

Amb `get_slide_collision(0)` obtenim la **primera col·lisió** que s'ha produït des de l'última crida a `move_and_slide()`. L'objecte que ens retorna conté informació detallada sobre la col·lisió, de la qual necessitarem el cos amb què hem col·lisionat i la normal de la col·lisió. 

Així:

* Amb `collission.get_collider()` obtenim l'objecte amb el què s'ha col·lissionat.
* Amb `collission.get_normal()` obtenim el vector normal a la col·lissió.

El vector *normal* ens indica la direcció perpendicular al punt de la superfície on s'ha produit la col·lissió. Així, si la normal és *horitzontal* indica que la col·lissió s'ha produit amb una paret vertical, per tant, seria l'equivalent al `is_in_wall()`:

```gdscript
if collision_normal.x != 0:
    direction = -direction  # Invertim la direcció (de dreta a esquerra o viceversa)
```

Ara cal comprovar si l'objecte amb el que s'ha produit la col·lissió és el jugador. Per a això, introduïm els grups.

!!!note Afegint el jugador al grup Player
     Els grups en Godot són una manera de classificar nodes per poder gestionar-los conjuntament. Això permet realitzar operacions a tots els nodes d'un grup de manera senzilla, com cridar un mètode o aplicar un canvi.
     
     A més, podem comprovar si un node forma part d'un grup amb el mètode `is_in_group("nom_grup")`.

     Així, abans de comprovar si es col·lissiona amb el jugador, anem a afegir aquest al grup *player*.

     Per a això, **obrim l'script del jugador, `player.gd`, i afegim la següent funció `_ready()`:

    ```gdscript
     func _ready():
	    self.add_to_group("player")
    ```

Així ara, amb `objecteCollissionat.is_in_group("player")` podem sabe si s'ha col·lissionat amb el jugador. Si és així, comprovem si la normal és *vertical*, el que indicaria que el jugador ha saltat a sobre l'enemic.

Després, la funció `queue_free()` el que fa és eliminar l'objecte de l'escena, i `get_tree.call_group())` el que fa és invocar un mètode concret sobre tots els objectes d'un grup, en aquest cal, la funció *reset_position* dels objectes del grup *player*.

Aquest funció *reset_position* no existeix encara al jugador, pel que **haurem d'editar de nou el fitxer `player.gd` i afegir aquesta funció**:

```gscript
func reset_position():
	position=Vector2(400, 225)
```

